1 CLS :SCREEN 7:MD$="BALL":LT=50:RT=318:OPSL=1


	OPEN "TENPAR" FOR INPUT AS#1
	INPUT #1,SPD
	CLOSE #1


2 DIM BALL(100),RAK(180)
3 	LOCATE 10,10:PRINT "PLESE SET :"
	LOCATE 11,10:PRINT "CAPS LOCK-ON"
	LOCATE 12,10:PRINT "NUM LOCK -ON"
	LOCATE 15,10:PRINT "PRESS <SPACE>"
4 A$=INKEY$
	IF A$=" " THEN GOTO 10
	GOTO 4

10 GOTO 500


20 CLS:SCREEN 7:RESTORE
21 DATA 0,0,1,1,1,1,1,1,0,0
22 DATA 0,1,1,1,1,1,1,1,1,0
23 DATA 1,1,1,1,1,1,1,1,1,1
24 DATA 1,1,1,1,1,1,1,1,1,1
25 DATA 1,1,1,1,1,1,1,1,1,1
26 DATA 1,1,1,1,1,1,1,1,1,1
27 DATA 1,1,1,1,1,1,1,1,1,1
28 DATA 0,1,1,1,1,1,1,1,1,0
29 DATA 0,0,1,1,1,1,1,1,0,0

30 FOR Y=1 TO 9
	FOR X=1 TO 10
	READ A
	IF A=1 THEN A=COL
	PSET (X,Y),A
	NEXT X
	NEXT Y
   IF MD$="BALL   " THEN GET (1,1)-(10,10),BALL

31 DATA 0,0,1,1,1,1,1,1,0,0
32 DATA 0,1,1,1,1,1,1,1,1,0
33 DATA 1,1,0,0,1,1,0,0,1,1
34 DATA 1,0,0,0,1,1,0,0,0,1
35 DATA 1,0,0,0,1,1,0,0,0,1
36 DATA 1,1,1,1,1,1,1,1,1,1
37 DATA 0,0,1,1,1,1,1,1,0,0
38 DATA 0,0,1,0,1,1,0,1,0,0
39 DATA 0,0,1,0,1,1,0,1,0,0

40 FOR Y=1 TO 9
	FOR X=1 TO 10
	READ A
	IF A=1 THEN A=COL
	PSET(X,Y),A
	NEXT X
	NEXT Y
	IF MD$="SCULL  " THEN GET (1,1)-(10,10),BALL


41 DATA 0,0,1,1,1,1,1,1,0,0
42 DATA 0,1,1,1,1,1,1,1,1,0
43 DATA 1,1,0,0,1,1,0,0,1,1
44 DATA 1,1,0,0,1,1,0,0,1,1
45 DATA 1,1,1,1,1,1,1,1,1,1
46 DATA 1,0,1,1,1,1,1,1,0,1
47 DATA 1,1,0,1,1,1,1,0,1,1
48 DATA 0,1,1,0,0,0,0,1,1,0
49 DATA 0,0,1,1,1,1,1,1,0,0

50 FOR Y=1 TO 9
	FOR X=1 TO 10
	READ A
	IF A=1 THEN A=COL
	PSET (X,Y),A
	NEXT X
	NEXT Y
	IF MD$="FACE   " THEN GET (1,1)-(10,10),BALL

51 DATA 1,0,0,0,1,1,0,0,0,1
52 DATA 0,1,0,0,1,1,0,0,1,0
53 DATA 0,0,1,0,1,1,0,1,0,0
54 DATA 0,0,0,1,1,1,1,0,0,0
55 DATA 1,1,1,1,1,1,1,1,1,1
56 DATA 0,0,0,1,1,1,1,0,0,0
57 DATA 0,0,1,0,1,1,0,1,0,0
58 DATA 0,1,0,0,1,1,0,0,1,0
59 DATA 1,0,0,0,1,1,0,0,0,1

60 FOR Y=1 TO 9
	FOR X=1 TO 10
	READ A
	IF A=1 THEN A=COL
	PSET (X,Y), A
	NEXT X
	NEXT Y
	IF MD$="SNOW   " THEN GET (1,1)-(10,10),BALL

61 DATA 0,0,1,1,1,1,1,1,0,0
62 DATA 0,1,1,0,0,0,0,1,1,0
63 DATA 1,1,0,0,0,0,0,0,1,1
64 DATA 1,0,0,0,0,0,0,0,0,1
65 DATA 1,0,0,0,0,0,0,0,0,1
66 DATA 1,0,0,0,0,0,0,0,0,1
67 DATA 1,1,0,0,0,0,0,0,1,1
68 DATA 0,1,1,0,0,0,0,1,1,0
69 DATA 0,0,1,1,1,1,1,1,0,0

70 FOR Y=1 TO 9
	FOR X=1 TO 10
	READ A
	IF A=1 THEN A=COL
	PSET (X,Y), A
	NEXT X
	NEXT Y
	IF MD$="ROUNDED" THEN GET (1,1)-(10,10),BALL

91 CLS:SCREEN 7

	LINE (1,2)-(60,4),4,BF
	LINE (1,2)-(20,1),8
	LINE (21,1)-(40,1),8
	LINE (41,1)-(60,2),8

	GET (1,1)-(60,4),RAK

100 CLS :SCREEN 7
	LINE (LT,1)-(RT,1),8
	IF AI<5 THEN  LINE (LT,2)-(LT-1,170),2,BF:LINE (RT,2)-(RT+1,170),2,BF
	IF AI>=5 THEN LINE (LT,2)-(LT-1,170),1,BF:LINE (RT,2)-(RT+1,170),1,BF
	SC=0
	TRY=1

110	XCH=0
	YCH=4
	XB=LT+10
	YB=10
	KR=LT+1
	IF AI=2 THEN XB=INT((LT+RT)/2)
	IF TRY>10 THEN GOTO 500

	PUT (XB,YB),BALL
	PUT (KR,171),RAK

115   A$=INKEY$
	IF A$="Q" THEN GOTO 500
	IF A$="1" THEN KH=-30 :CHAN=CHAN+1
	IF A$="3" THEN KH=30  :CHAN=CHAN+1
	IF A$="4" THEN KH=-45 :CHAN=CHAN+1
	IF A$="6" THEN KH=45  :CHAN=CHAN+1
	IF A$="7" THEN KH=-60 :CHAN=CHAN+1
	IF A$="9" THEN KH=60  :CHAN=CHAN+1
	IF A$=""  THEN KH=0   :CHAN=CHAN+1
	IF A$=" " THEN GOSUB 150
	RCH=RCH+KH
	IF ABS(XCH)>10 AND OPSL=1 THEN SPG=SPD/2
	IF ABS(XCH)<=10 THEN SPG=SPD/5
	FOR T=1 TO SPG:NEXT T
	IF CHAN<20 THEN GOTO 115
	CHAN=0
	KH=0

	PUT (XB,YB),BALL
	XB=XB+XCH

	IF XB>RT-10 AND AI<5 THEN XB=RT-10:XCH=-XCH:SOUND 400,0.8
	IF XB<LT  AND AI<5 THEN XB=LT :XCH=-XCH:SOUND 400,0.8

	IF XB>RT-10 AND AI>=5 THEN XB=LT :SOUND 250,0.8
	IF XB<LT  AND AI>=5 THEN XB=RT-10:SOUND 250,0.8


	YB=YB+YCH
	IF YB<1 THEN YB=1:YCH=-YCH:SOUND 500,0.8:GOSUB 122
	IF YB>162 THEN YB=162:GOTO 130

121	PUT(XB,YB),BALL
	PUT (KR,171),RAK

	KR=KR+RCH
	IF KR<LT+1 THEN KR=LT+1
	IF KR>RT-60 THEN KR=RT-60
	PUT (KR,171),RAK
	RCH=0
	LOCATE 1,1:PRINT SC
	LOCATE 3,1:PRINT "BONUS:"
	LOCATE 4,1:PRINT ABS(XCH)
	LOCATE 5,1:PRINT "TRY:"
	LOCATE 6,1:PRINT TRY
	GOTO 115

122   IF AI=2 THEN GOTO 124
	IF AI=1 THEN GOTO 123
	IF AI=3 THEN GOTO 125
	IF AI=4 THEN GOTO 126
	IF AI=6 THEN GOTO 123
	IF AI=7 THEN GOTO 124
	IF AI=8 THEN GOTO 125
	IF AI=9 THEN GOTO 126
	RETURN

123   IF ABS(XCH)>ABS(MT) THEN RETURN
	IF XCH>0 THEN XCH=XCH+INT(RND*MT):RETURN
	IF XCH<0 THEN XCH=XCH-INT(RND*MT):RETURN
	XCH=INT(9*RND)-4
	RETURN

124   XCH=-XCH:RETURN

125   XCH=-XCH
	IF ABS(XCH)>MT THEN RETURN
	IF XCH<0 THEN XCH=XCH-1:RETURN
	IF XCH>0 THEN XCH=XCH+1:RETURN
	XCH=INT(RND*MT)-MT/2:RETURN

126   IF YCH<MT THEN YCH=YCH+1:RETURN
	RETURN

130   SP=INT(2*(ABS(RCH))/3 )
	MP=INT(2*(ABS(RCH))/5)
	BN=ABS(XCH)
	IF SP=0 THEN SP=3
	IF MP=0 THEN MP=2

	IF XB<KR-10 THEN GOTO 140
	IF XB>KR+60 THEN GOTO 140

	SOUND 400,0.8

	IF XB+5>=KR+20 AND XB+5<=KR+40  THEN YCH=-YCH:SC=SC+BN:GOTO 121
	IF XB+5>KR+40  AND XB+5<KR+60   THEN YCH=-YCH:XCH=XCH+MP:SC=SC+BN:GOTO 121
	IF XB+5<KR+20  AND XB+5>KR      THEN YCH=-YCH:XCH=XCH-MP:SC=SC+BN:GOTO 121

	IF XB+5 >=KR-10 AND XB+5<=KR+10 THEN YCH=-YCH:XCH=XCH-SP:SC=SC+BN:GOTO 121
	IF XB+5<=KR+60  AND XB+5>=KR+50 THEN YCH=-YCH:XCH=XCH+SP:SC=SC+BN:GOTO 121
	IF XB<KR+60     AND XB>KR+55    THEN YCH=-YCH:XCH=XCH+2*MP:SC=SC+BN:GOTO 121
	IF XB+10>KR     AND XB+10<KR+5  THEN YCH=-YCH:XCH=XCH-2*MP:SC=SC+BN:GOTO 121
140
	VB=1
	PUT (XB,YB),BALL
	FOR T=1 TO SPD*20:NEXT T
	PUT (XB,YB),BALL
	XB=XB+INT(XCH/2)
	IF XB<1 THEN XB=1
	IF XB>300 THEN XB=300
	YB=YB+INT(YCH/2)
	IF YB>180 THEN YB=180:VB=0
	IF VB=1 THEN GOTO 140
	FOR T=1 TO SPD *12:NEXT T
	SOUND 200,0.8
	PUT (KR,171),RAK
	PUT (XB,YB),BALL
	TRY=TRY+1
	GOTO 110

150  A$=INKEY$
	IF A$=" " THEN RETURN
	GOTO 150


500   LINE (1,1)-(5,5),7,BF
	GET (1,1)-(5,5),BALL
	CLS:SCREEN 7
	FOR Y=1 TO 70
	LINE (1,Y)-(320,Y),8
	IF Y>1 AND Y<70 THEN LINE(1,Y-1)-(320,Y-1),0
	IF Y>1 AND Y<70 THEN LINE(1,190-Y)-(320,190-Y),0
	LINE (1,189-Y)-(320,189-Y),8
	FOR X=1 TO 140000:NEXT X
	NEXT Y
	SOUND 400,0.8
	LOCATE 10,7:PRINT "<=====< T E N N I S >=====>"
	LOCATE 13,15:PRINT "<SPACE> TO START"
	LOCATE 14,15:PRINT "<Q> TO EXIT"
	LOCATE 17,10:PRINT "BORYA PROGRAMICS.inc"
	LOCATE 21,5 :PRINT "VER:1.00 - 2D GRAPHICS"
	LINE (1,49)-(318,49),8
	LINE (1,1)-(1,49),4
	LINE (319,1)-(319,49),4
	LINE (1,1)-(319,1),4
	XCAV=2
	XCH=2
	YCH=1
	XB=1
	YB=1
	PUT (1,1),BALL


505   A$=INKEY$
	IF A$=" " THEN GOTO 510
	IF A$="Q" THEN END
	PUT (XB,YB),BALL
	XB=XB+XCH
	IF XB<1 THEN XB=1:XCH=-XCH
	IF XB>315 THEN XB=315:XCH=-XCH
	YB=YB+YCH
	IF YB<1 THEN YB=1:YCH=-YCH
	IF YB>44 THEN YB=44:YCH=-YCH
	PUT (XB,YB),BALL
	XV=XV+1
	IF XV=310 THEN XV=2:CL=CL+1:SOUND 500,0.8
	IF CL>8 THEN CL=1
	LINE (XV,50)-(XV,68),14
	LINE (XV-2,50)-(XV-2,68),0
	LINE (316-XV,50)-(316-XV,68),14
	LINE (318-XV,50)-(318-XV,68),0
	FOR T=1 TO SPD*1.5:NEXT T
	LINE (50,140)-(270,125),CL,B
	GOTO 505

510   FOR T=1 TO SPD*20:NEXT T
	CLS:SCREEN 7:COL=1:AI=0:MD$="BALL   ":MT=0
	LOCATE 2,10:PRINT "GAME PARAMETRS"

	YCUR=4

	PRINT "---------------------------------------"
	PRINT " ==>BALL  MODE    : BALL"
	PRINT "    BALL CLOUR    : BLUE"
	PRINT "    AI MODE       : NORMAL"
	PRINT "    PLUS PARAMETER: 0"
	PRINT "    START THE GAME >>>"
	PRINT "    MORE   OPTIONS >>>"
	PRINT "    EXIT  THE GAME >>>"
	PRINT "---------------------------------------"
	'21


520 A$=INKEY$
	IF A$<>"" THEN SOUND 500,0.8
	IF A$="2" THEN GOTO 521
	IF A$="5" THEN GOTO 522
	IF A$=" " THEN GOTO 540
	IF A$="Q" OR A$="q" THEN END
	GOTO 520
521   LOCATE YCUR,2:PRINT "   "
	YCUR=YCUR+1
	IF YCUR>10 THEN YCUR=4
	LOCATE YCUR,2:PRINT "==>"
	GOTO 520
522   LOCATE YCUR,2:PRINT "   "
	YCUR=YCUR-1
	IF YCUR<4 THEN YCUR=8
	LOCATE YCUR,2:PRINT "==>"
	GOTO 520

540   IF YCUR=4 THEN GOTO 545
	IF YCUR=5 THEN GOTO 550
	IF YCUR=6 THEN GOTO 555
	IF YCUR=7 THEN GOTO 560
	IF YCUR=8 THEN GOTO 541
	IF YCUR=9 THEN GOTO 570
	IF YCUR=10 THEN GOTO 590
	GOTO 540
541   IF AI=1 AND MT=0 THEN GOTO 520
	IF AI=3 AND MT=0 THEN GOTO 520
	IF AI=4 AND MT=0 THEN GOTO 520
	IF AI=6 AND MT=0 THEN GOTO 520
	IF AI=8 AND MT=0 THEN GOTO 520
	IF AI=9 AND MT=0 THEN GOTO 520
	GOTO 20

545   IF MD$="BALL   " THEN MR$="SCULL  "
	IF MD$="SCULL  " THEN MR$="FACE   "
	IF MD$="FACE   " THEN MR$="SNOW   "
	IF MD$="SNOW   " THEN MR$="ROUNDED"
	IF MD$="ROUNDED" THEN MR$="BALL   "
	MD$=MR$
	LOCATE 4,21:PRINT MD$
	GOTO 520

550   IF COL=1 THEN CL=2 : BLC$="GREEN"
	IF COL=2 THEN CL=4 : BLC$="RED  "
	IF COL=4 THEN CL=6 : BLC$="GRAY "
	IF COL=6 THEN CL=7 : BLC$="WHITE"
	IF COL=7 THEN CL=8 : BLC$="DUST "
	IF COL=8 THEN CL=1 : BLC$="BLUE "
	COL=CL
	LOCATE 5,21:PRINT BLC$
	LINE (230,25)-(250,35),COL,BF
	GOTO 520

555   AI=AI+1
	IF AI>9 THEN AI=0
	IF AI=0 THEN AD$="NORMAL           "
	IF AI=1 THEN AD$="MAD              "
	IF AI=2 THEN AD$="UNREAL           "
	IF AI=3 THEN AD$="UNREAL PLUS      "
	IF AI=4 THEN AD$="SPEED INCREASE   "
	IF AI=5 THEN AD$="PORTAL           "
	IF AI=6 THEN AD$="PORTAL+MAD       "
	IF AI=7 THEN AD$="PORTAL+UNREAL    "
	IF AI=8 THEN AD$="PORTAL+UNREAL PL."
	IF AI=9 THEN AD$="PORTAL+SPEED INC."
	LOCATE 6,21:PRINT AD$
	GOTO 520

560   LOCATE 7,21
	LINE INPUT "";MT$
	IF MT$="BORYA" THEN PAS$="BORYA":GOTO 560
	MT=VAL(MT$)
	GOTO 520
570   LOCATE 12,1:PRINT "  <1>- LEFT:";LT
	LOCATE 13,1:PRINT "  <2>-RIGHT:";RT
	LOCATE 14,1:PRINT "  <3>-SPEED:";SPD
	LOCATE 15,1:PRINT "  <4>-BACK"
	LOCATE 16,1:PRINT "  <5>-HELP"
	LOCATE 17,1:PRINT "  <6>-AUTO SLOWING:"
572 A$=INKEY$
	IF A$=""  THEN GOTO 572
	IF A$="1" THEN GOTO 580
	IF A$="2" THEN GOTO 581
	IF A$="3" THEN GOTO 585
	IF A$="4" THEN GOTO 582
	IF A$="5" THEN GOTO 600
	IF A$="6" THEN GOTO 583
	GOTO 572
580   LOCATE 12,14:PRINT "    "
	LOCATE 12,14:LINE INPUT LT$
	LT=VAL(LT$)
	LOCATE 12,14:PRINT LT
	GOTO 572
581   LOCATE 13,14:PRINT "    "
	LOCATE 13,14:LINE INPUT RT$
	RT=VAL(RT$)
	LOCATE 13,14:PRINT RT
	GOTO 572
582   IF RT>318    THEN GOTO 572
	IF LT<50     THEN GOTO 572
	IF RT-LT<100 THEN GOTO 572
	LOCATE 12,1:PRINT "                             "
	LOCATE 13,1:PRINT "                             "
	LOCATE 14,1:PRINT "                             "
	LOCATE 15,1:PRINT "                             "
	LOCATE 16,1:PRINT "                             "
	LOCATE 17,1:PRINT "                             "
	GOTO 520
583   IF OPSL=1 THEN OPSL=0 :OP$="  <6>-AUTO SLOWING:OFF":GOTO 584
	IF OPSL=0 THEN OPSL=1 :OP$="  <6>-AUTO SLOWING:ON ":GOTO 584
584   LOCATE 17,1:PRINT OP$
	GOTO 572

585   LOCATE 14,14:PRINT "            "
	LOCATE 14,14:LINE INPUT SPD$
	SPD=VAL(SPD$)
	IF SPD<=1000 THEN GOTO 585
	OPEN "TENPAR" FOR OUTPUT AS #1
	PRINT #1,SPD
	CLOSE #1
	GOTO 572

590   CLS:SCREEN 7
	LOCATE 10,15:PRINT "BYE-BYE"
	LOCATE 12,10:PRINT "PRESS <SPACE>"
591   A$=INKEY$
	IF A$=" " THEN END
	GOTO 591

600   CLS:SCREEN 7
	IF PAS$="BORYA" THEN GOTO 602
	LOCATE 2,10:PRINT "WELCOME TO THE HELP"
	PRINT:PRINT
	PRINT "LEFT/RIGHT-ROCKET MOVES:"
	PRINT "<1> / <3> -HALF ROKCET LENTH MOVE"
	PRINT "<4> / <6> -3/4 ROCKET LENTH MOVE"
	PRINT "<7> / <9> -ROCKET LENTH MOVE"
	PRINT
	PRINT "<SPACE>   -PAUSE THE GAME"
	PRINT "  <Q>     -EXIT THE GAME DURING PLAY"
	PRINT
	PRINT " MOWING ROCKET WHEN BALL TOUCHES IT,"
	PRINT "INCREASES BALL SPEED IN MOVE DIRECTION"
	PRINT:PRINT "PRESS <SPACE> FOR EXIT "
601   A$=INKEY$
	IF A$=" " THEN GOTO 510
	GOTO 601
602   CLS:SCREEN 7
	LOCATE 2,10: PRINT "WELCOME TO THE HELL"
	PRINT
	PRINT "PRIVET VSEM ZEMLYANAM NA PLANETE ZEMLYA"
	PRINT
	PRINT "  PRESS <SPACE> TO BE KICKED AWAY "
603   A$=INKEY$
	IF A$=" " THEN GOTO 510
	GOTO 603